import * from "std/env"
import * from "../utils.ab"
import * from "../utils/truecolor.ab"
import * from "../utils/term.ab"

/// Renders the Yes/No selection with background colors
/// Uses smaller buttons when terminal width is below 30 characters
fun render_confirm_options(selected: Bool, term_width: Num): Null {
    let small = term_width < 30
    let yes_label = small then " Yes " else "    Yes    "
    let no_label = small then " No " else "    No    "
    let gap = small then " " else "  "

    eprintf(" ")
    if selected {
        // Yes selected
        eprintf("\e[97m" + background_secondary(yes_label))
        eprintf(gap)
        // No not selected (dim)
        eprintf("\e[49;37m" + no_label + "\e[0m")
    } else {
        // No selected
        eprintf("\e[49;37m" + yes_label + "\e[0m")
        eprintf(gap)
        eprintf("\e[97m" + background_secondary(no_label))
    }
}

/// Prompts the user with a Yes/No confirmation dialog.
///
/// ### Parameters
/// - `header`: The header message to display above the options. (ANSI supported)
/// - `default_yes`: Whether Yes is selected by default. Default is true.
///
/// ### Returns
/// - `true` if the user selected Yes, `false` if No.
pub fun xyl_confirm(
    header: Text = "Are you sure?",
    default_yes: Bool = true,
): Bool {
    trust $ stty -echo < /dev/tty $
    hide_cursor()

    let term_width = term_width()

    if header != "" {
        eprintf(truncate_text(header, term_width) + "\n\n")
    } else {
        new_line(1)
    }

    let selected = default_yes

    // Render initial options
    render_confirm_options(selected, term_width)

    eprintf("\n\n")
    // "←→ select • enter confirm • y yes • n no" = 9 + 3 + 13 + 3 + 5 + 3 + 4 = 40
    render_tooltip(["←→", "select", "enter", "confirm", "y", "yes", "n", "no"], 40, term_width)
    go_up(2)

    loop {
        let key = get_key()

        if {
            key == "LEFT" or key == "h" or key == "RIGHT" or key == "l" {
                if {
                    selected {
                        selected = false
                        eprintf("\e[9999D\e[K")
                        render_confirm_options(selected, term_width)
                    }
                    not selected {
                        selected = true
                        eprintf("\e[9999D\e[K")
                        render_confirm_options(selected, term_width)
                    }
                }
            }
            key == "y" or key == "Y" {
                selected = true
                break
            }
            key == "n" or key == "N" {
                selected = false
                break
            }
            key == "INPUT" {
                break
            }
            else: continue
        }
    }

    // Clean up: remove options line and hint line
    let total_lines = 4
    if header != "" {
        total_lines += 1
    }

    go_down(2)
    remove_line(total_lines - 1)
    remove_current_line()

    trust $ stty echo < /dev/tty $
    show_cursor()

    return selected
}
