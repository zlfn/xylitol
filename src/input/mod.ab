import * from "std/env"
import * from "std/math"
import * from "../utils.ab"

/// Prompts the user single-line input with a placeholder.
///
/// ### Parameters
/// - `prompt`: The prompt message to display to the user.
/// - `placeholder`: The placeholder text to show in the input field.
/// - `header`: An optional header message to display above the prompt. (ANSI supported)
/// - `password`: A boolean indicating whether the input should be masked.
pub fun xyl_input(
    prompt: Text, 
    placeholder: Text,
    header: Text = "",
    password: Bool = false,
): Text {
    let term_width = get_term_width()

    if header != "" {
        eprintf(truncate_text(header, term_width) + "\n")
    }

    new_line(2)
    // "enter submit" = 12
    render_tooltip(["enter", "submit"], 12, term_width)
    go_up(2)
    eprintf("\e[99999D")

    eprintf(prompt)
    eprintf_colored(placeholder, 90)

    trust $ stty -echo < /dev/tty $
    let char = get_char()
    remove(len(prompt))
    remove(len(placeholder) + 1)

    let text = ""
    if not password {
        trust $ stty echo < /dev/tty $
        text = trust $ read -e -i {char} -p "{prompt}" text < /dev/tty; printf "%s" "\$text" $
    } else {
        trust $ stty echo < /dev/tty $
        text = trust $ read -es -i {char} -p "{prompt}" text < /dev/tty; printf "%s" "\$text" $
    }

    trust $ stty -echo < /dev/tty $

    // Calculate how many lines the input takes up (prompt + text may wrap)
    let term_width = get_term_width()
    let input_display_len = len(prompt + text)
    let input_lines = math_ceil(input_display_len / term_width)

    if input_lines < 3 {
        go_down(1)
        remove_line(2)
        remove_current_line()
    }
    if input_lines >= 3 {
        remove_line(input_lines)
    }
    if header != "" {
        remove_line(1)
        remove_current_line()
    }

    trust $ stty echo < /dev/tty $
    return text
}
