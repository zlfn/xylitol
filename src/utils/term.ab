import { split, parse_int } from "std/text"

/// global variables to store terminal size
/// (prevent multiple queries in one session)
let _got_term_size = false
let _term_size = [80, 24]

/// stty lock/unlock using environment variable for cross-module state
trust $ export XYLITOL_RUNTIME_STTY_COUNT=0 $

/// Lock the terminal (disable echo). Uses reference counting via environment variable.
pub fun stty_lock(): Null {
    let count = trust $ echo "\$\{XYLITOL_RUNTIME_STTY_COUNT:-0}" $
    let count_num = parse_int(count) failed { 0 }
    if count_num == 0 {
        trust $ stty -echo < /dev/tty $
    }
    count_num += 1
    trust $ export XYLITOL_RUNTIME_STTY_COUNT={count_num} $
}

/// Unlock the terminal (enable echo). Uses reference counting via environment variable.
pub fun stty_unlock(): Null {
    let count = trust $ echo "\$\{XYLITOL_RUNTIME_STTY_COUNT:-0}" $
    let count_num = parse_int(count) failed { 0 }
    if count_num > 0 {
        count_num -= 1
        trust $ export XYLITOL_RUNTIME_STTY_COUNT={count_num} $
        if count_num == 0 {
            trust $ stty echo < /dev/tty $
        }
    }
}

/// Get the terminal size (columns, rows) using ANSI escape sequence.
fun get_term_size(): Null? {
    // Query terminal size with \x1b[18t, response format: \x1b[8;rows;colst
    let result = trust $ printf '\x1b[18t' > /dev/tty; IFS=';' read -rsd t _ignore height width < /dev/tty; echo "\$height; \$width" $
    let parts = split(result, ";")
    if len(parts) != 2 {
        fail
    }
    let rows = parse_int(parts[0])?
    let cols = parse_int(parts[1])?

    _term_size = [cols, rows]
    _got_term_size = true
}

pub fun term_size(): [Int] {
    if not _got_term_size {
        trust get_term_size()
    }
    return _term_size
}

/// Get the terminal width (columns).
pub fun term_width(): Int {
    if not _got_term_size {
        trust get_term_size()
    }
    return _term_size[0]
}

/// Get the terminal height (rows).
pub fun term_height(): Int {
    if not _got_term_size {
        trust get_term_size()
    }
    return _term_size[1]
}
